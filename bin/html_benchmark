#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Euclid;
use Config::Std;
use HTML::Benchmark;
use HTML::Benchmark::Output::Form;
use Readonly;
use File::HomeDir;

Readonly my $config_file        => $ARGV{'--config'}
                                 ? $ARGV{'--config'}
                                 : File::HomeDir->my_home.'/.html_benchmarkrc';
my %config;
if (-r $config_file) {
    read_config $config_file => %config;
}

my %args = ();
my $iterations = get_arg('iterations');
$args{label} = get_arg('label');

my $ua = HTML::Benchmark->new(%args);
if (get_arg('benchmark')) {
    $ua->statistics->set_benchmark(get_arg('benchmark'));
}

my @pages = get_arg('page');
foreach my $s (get_arg('sitemap')) {
    push @pages, $ua->split_sitemap($s);
}
if (not @pages) {
    push @pages, 'http://localhost/';
}

for(my $i = 0; $i < $iterations; $i++) {
    foreach my $p (@pages) {
        $ua->benchmark($p);
    }
}

my $output = HTML::Benchmark::Output::Form->new($ua, %{$config{Form}});
print $output->output;
exit(0);

sub get_arg {
    my $name = shift;
    my $value = $config{main}{$name};
    if (not $value) {
        $value = $ARGV{"--$name"};
    }
    if (wantarray and not $value) {
        return;
    }
    elsif (wantarray and ref $value eq 'ARRAY') {
        return @$value;
    }
    return $value;
}

__END__

=head1 NAME

html_benchmark

=head1 VERSION

This documentation refers to html_benchmark 0.0.1.

=head1 DESCRIPTION

C<html_benchmark> is similar to the Apache utility, C<ab>,
or the firebug tool that allow you to analyse the performance
of websites.

=head1 USAGE

    html_benchmark [options]

=head1 OPTIONS

=over

=item --config <file>

Specify an alternative file location for a config file rather
than the C<.html_benchmarkrc> file in the home directory. Most of the
main options can be set default values in the main section of config file.

=item --page <url>...

Add web pages to test. If neither I<--page> nor I<--sitemap>
options are used a default of "http://localhost/" is assumed.
This option can also be set by the I<page> option in the 
main section of the config file.

=for Euclid:
    url.type: /http\:\/\/[\w\-\/\?\&\%\.]+/

=item --sitemap <url>...

Add the pages listed in the specified sitemaps.
This option can also be set by the I<sitemap> option in the 
main section of the config file.

=for Euclid:
    url.type: /http\:\/\/[\w\-\/\?\&\%\.]+/

=item --benchmark <i>

Set a maximal mean download time above which a warning will be written 
to standard output. This check is done during the calculation of statistics.

=for Euclid:
    i.type: +number

=item --label <s>

Label to group related tests. This defaults to "TEST".
An alternative default can be set by the I<label> field
in the main section of the config file.

=for Euclid:
    s.type: string
    s.default: "TEST"

=item --iterations <i>

Number of times to run experiment per page. This defaults to 10.
An alternative default can be set by the I<iterations> field
in the main section of the config file.

=for Euclid:
    i.type: +integer
    i.default: 10

=item --version

=item --usage

=item --help

=item --man

=back

=head1 BUGS AND LIMITATIONS

No bugs have been reported.

Please report any bugs or feature requests to
C<bug-html-benchmark@rt.cpan.org>, or through the web interface at
L<http://rt.cpan.org>.

=head1 AUTHOR

Nicholas Bamber  C<< <nicholas@periapt.co.uk> >>

=head1 LICENCE AND COPYRIGHT

Copyright (c) 2010, Nicholas Bamber (http://www.periapt.co.uk). All rights reserved.

This module is free software; you can redistribute it and/or
modify it under the same terms as Perl itself.

=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
NECESSARY SERVICING, REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE
LIABLE TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL,
OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE
THE SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF
SUCH DAMAGES.
